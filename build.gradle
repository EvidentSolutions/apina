plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.2.4'
}

scmVersion {
    localOnly = true
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
}

configure(allprojects) { project ->
    group = 'fi.evident.apina'
    project.version = scmVersion.version

    ext {
        slf4jVersion = '1.7.12'
    }

    repositories {
        jcenter()
    }

    configurations.all {
        resolutionStrategy.dependencySubstitution.eachModule { ModuleDependencySubstitution details ->
            if (details.requested.group == 'org.slf4j') {
                details.useVersion slf4jVersion
            }
        }
    }
}

configure(subprojects) { project ->
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        compile "org.slf4j:slf4j-api"
        compile "org.jetbrains:annotations:13.0"
        testCompile "junit:junit:4.12"
    }
}

project(':apina-tsang') {
    dependencies {
        compile project(':apina-model')
    }
}

project(':apina-java') {
    dependencies {
        compile "org.ow2.asm:asm:5.0.4"
    }
}

project(':apina-spring') {
    dependencies {
        compile project(':apina-model')
        compile project(':apina-java')
    }
}

project(':apina-cli') {
    dependencies {
        compile project(':apina-model')
        compile project(':apina-spring')
        compile project(':apina-tsang')
        compile "ch.qos.logback:logback-classic:1.1.3"
    }
}
