buildscript {
    ext {
        kotlinVersion = '1.0.6'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.2.4'
}

scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
}

configure(allprojects) { project ->
    group = 'fi.evident.apina'
    project.version = scmVersion.version

    ext {
        slf4jVersion = '1.7.12'
    }

    repositories {
        jcenter()
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                if (details.requested.group == 'org.slf4j') {
                    details.useVersion slf4jVersion
                } else if (details.requested.name == 'hamcrest-core') {
                    details.useTarget group: details.requested.group, name: 'hamcrest-all', version: details.requested.version
                }
            }
        }
    }
}

configure(subprojects) { project ->
    apply plugin: 'kotlin'
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

task publish {
    group = 'publishing'
    description = 'Publishes all artifacts'

    dependsOn tasks.findByPath(':apina-gradle:publishPlugins')
    dependsOn tasks.findByPath(':apina-core:bintrayUpload')
}

